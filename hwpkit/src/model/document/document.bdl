// BDL port of hwpkit document model.
// NOTE: TypeScript definitions remain source-of-truth for runtime for now.

custom Index = integer
custom Hwpunit = number
custom RgbColor = integer
custom Percent = number

struct DocumentModel {
  head: Head,
  body: Body,
}

// ====================
// Head
// ====================

struct Head {
  docSetting: DocumentSetting,
  mappingTable: MappingTable,
}

struct DocumentSetting {
  beginNumber: BeginNumber,
  caretPos: CaretPos,
}

struct BeginNumber {
  endnote: integer,
  equation: integer,
  footnote: integer,
  page: integer,
  picture: integer,
  table: integer,
}

struct CaretPos {
  list: integer,
  para: integer,
  pos: integer,
}

struct MappingTable {
  fontFaces: Font[][LangType],
  charShapes: CharShape[],
  paraShapes: ParaShape[],
  styles: Style[],
}

struct Style {
  type: StyleType,
  name: string,
  engName: string,
  paraShapeIndex: Index,
  charShapeIndex: Index,
  nextStyleIndex: Index,
}

enum StyleType {
  Para,
  Char,
}

enum LangType {
  Hangul,
  Latin,
  Hanja,
  Japanese,
  Other,
  Symbol,
  User,
}

struct Font {
  name: string,
  type?: FontType,
}

enum FontType {
  Rep,
  Ttf,
  Hft,
}

struct CharShape {
  height: Hwpunit,
  textColor: RgbColor,
  shadeColor: RgbColor,
  useFontSpace: boolean,
  useKerning: boolean,
  fontIds: Index[LangType],
  ratios: Percent[LangType],
  charSpacings: Percent[LangType],
  relSizes: Percent[LangType],
  charOffsets: Percent[LangType],
  italic: boolean,
  bold: boolean,
  underline?: Underline,
  strikeout?: Strikeout,
  outline?: Outline,
  shadow?: Shadow,
  emboss: boolean,
  engrave: boolean,
  superscript: boolean,
  subscript: boolean,
}

struct Underline {
  type: UnderlineType,
  shape: LineType2,
  color: RgbColor,
}

enum UnderlineType {
  Bottom,
  Center,
  Top,
}

struct Strikeout {
  type: StrikeoutType,
  shape: LineType2,
  color: RgbColor,
}

enum StrikeoutType {
  None,
  Continuous,
}

struct Outline {
  type: LineType3,
}

struct Shadow {
  type: ShadowType,
  color: RgbColor,
  offsetX: Percent,
  offsetY: Percent,
}

enum ShadowType {
  Drop,
  Cont,
}

enum LineType1 {
  Solid,
  Dash,
  Dot,
  DashDot,
  Circle,
  DoubleSlim,
  SlimThick,
  ThickSlim,
  SlimThickSlim,
  None,
}

enum LineType2 {
  Solid,
  Dash,
  Dot,
  DashDot,
  Circle,
  DoubleSlim,
  SlimThick,
  ThickSlim,
  SlimThickSlim,
}

enum LineType3 {
  Solid,
  Dot,
  Thick,
  Dash,
  DashDot,
}

enum AlignmentType1 {
  Justify,
  Left,
  Right,
  Center,
  Distribute,
  DistributeSpace,
}

enum AlignmentType2 {
  Left,
  Center,
  Right,
}

enum VerAlignType {
  Baseline,
  Top,
  Center,
  Bottom,
}

struct ParaShape {
  align: AlignmentType1,
  verAlign: VerAlignType,
  headingType: HeadingType,
  level: integer,
  tabDef: Index,
  breakLatinWordType: BreakLatinWordType,
  breakNonLatinWord: boolean,
  condense: Percent,
  widowOrphan: boolean,
  keepWithNext: boolean,
  keepLines: boolean,
  pageBreakBefore: boolean,
  fontLineHeight: boolean,
  snapToGrid: boolean,
  lineWrapType: LineWrapType,
  autoSpaceEAsianEng: boolean,
  autoSpaceEAsianNum: boolean,
}

enum HeadingType {
  None,
  Outline,
  Number,
  Bullet,
}

enum BreakLatinWordType {
  KeepWord,
  Hyphenation,
  BreakWord,
}

enum LineWrapType {
  Break,
  Squeeze,
  Keep,
}

// ====================
// Body
// ====================

struct Body {
  sections: Section[],
}

struct Section {
  def: SecDef,
  paragraphs: Paragraph[],
}

struct SecDef {
  textDirection: TextDirection,
  spaceColumns: Hwpunit,
  tabStop: Hwpunit,
  pageDef: PageDef,
}

enum TextDirection {
  Horizontal,
  Vertical,
}

struct PageDef {
  landscape: boolean,
  width: Hwpunit,
  height: Hwpunit,
  gutterType: GutterType,
  margin: PageMargin,
}

enum GutterType {
  LeftOnly,
  LeftRight,
  TopBottom,
}

struct PageMargin {
  left: Hwpunit,
  right: Hwpunit,
  top: Hwpunit,
  bottom: Hwpunit,
  header: Hwpunit,
  footer: Hwpunit,
  gutter: Hwpunit,
}

struct Paragraph {
  paraShapeIndex: Index,
  styleIndex: Index,
  instId: integer,
  pageBreak: boolean,
  columnBreak: boolean,
  texts: Text[],
  colDef?: ColDef,
}

struct Text {
  charShapeIndex: Index,
  controls: Control[],
}

union Control {
  CharControl(code: integer),
}

struct ColDef {
  type: ColType,
  count: integer,
  layoutType: ColLayoutType,
  sameSize: boolean,
  sameGap: Hwpunit,
  columns: Column[],
}

enum ColType {
  Newspaper,
  BalancedNewspaper,
  Parallel,
}

enum ColLayoutType {
  Left,
  Right,
  Mirror,
}

struct Column {
  width: Hwpunit,
  gap: Hwpunit,
}
